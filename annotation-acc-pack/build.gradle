apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    def acceleratorVersion = '1.0.5'
    compileSdkVersion 30
    buildToolsVersion "30.0.0"
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.3"
        project.archivesBaseName = "opentok-accelerator-annotation"
        project.version = acceleratorVersion
        testApplicationId "com.tokbox.android.annotations.test"
        testInstrumentationRunner "com.tokbox.android.annotations.testbase.TestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField 'String', 'acceleratorVersion', '\"'+acceleratorVersion+'\"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            buildConfigField 'String', 'acceleratorVersion', '\"'+acceleratorVersion+'\"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
    }

    libraryVariants.all { variant ->

        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFileName = fileName
            }
        }

        def javadoc = task("generateJavaDoc${variant.name.capitalize()}", type: Javadoc, dependsOn: "generate${variant.name.capitalize()}Sources") {
            source = 'src/main/java'
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            destinationDir = file("build/outputs/docs/")
            title = 'OpenTok Android Annotations'
            exclude "**/config/**"
        }

        def zipBundle = task("zipBundle${variant.name.capitalize()}", type: Zip) {
            from ('build/outputs')
            include '**/aar/**'
            include '**/docs/**'
            exclude '**/lint-results-debug_files/**'
            destinationDirectory = file(".")
            archiveFileName = "${archivesBaseName}-${version}.zip"
        }

        task("printVersion${variant.name.capitalize()}") {
          println project.version
        }

        zipBundle.dependsOn build
        zipBundle.dependsOn javadoc

    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
    }
}

if (project.hasProperty("MAVEN_USERNAME") && project.hasProperty("MAVEN_PASSWORD")) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://api.bintray.com/maven/tokbox/maven/${ARTIFACT_ID}/;publish=1") {
                    authentication(userName: "$MAVEN_USERNAME" ? "$MAVEN_USERNAME" : "", password: "$MAVEN_PASSWORD" ? "$MAVEN_PASSWORD" : "")
                }
                def (revision, buildTime) = getRevisionAndTime()
                pom.version = "${version}"
                pom.groupId = 'com.opentok.android'
                pom.artifactId = "${ARTIFACT_ID}"
            }
        }
    }
}

def checkMaven = task("checkUploadArchivesDeps") {
    doFirst {
        if (!project.hasProperty("MAVEN_USERNAME")) {
            throw new GradleException("MAVEN_USERNAME Property not defined")
        }
        if (!project.hasProperty("MAVEN_PASSWORD")) {
            throw new GradleException("MAVEN_PASSWORD Property not defined")
        }
    }
}

uploadArchives.dependsOn checkMaven

def getRevisionAndTime() {
    def date = new Date()
    def hash = 'git rev-parse --verify HEAD'.execute().text.trim()
    TimeZone.setDefault(TimeZone.getTimeZone('PST'))
    def formattedDate = date.format('yyyyMMddHHmmss')
    return ["${hash}".toString(), formattedDate.toString()]
}

dependencies {
    repositories {
      maven { url 'http://tokbox.bintray.com/maven' }
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13'
    androidTestCompile files('src/androidTest/libs/android-junit-report-1.5.8.jar')
    implementation 'com.opentok.android:opentok-accelerator-core:1.0.22'
}
